Перем Лог;



Функция ФайлСуществует(Знач пИмяФайла) Экспорт
	
	файл = Новый Файл(УбратьКавычки(пИмяФайла));
	
	Возврат файл.Существует()
	И Не файл.ЭтоКаталог();
	
КонецФункции

Функция КаталогСуществует(Знач пИмяФайла) Экспорт
	
	файл = Новый Файл(УбратьКавычки(пИмяФайла));
	
	Возврат файл.Существует()
	И файл.ЭтоКаталог();
	
КонецФункции

Функция АбсолютныйПуть(Знач пИмяФайла) Экспорт
	
	Попытка
		
		файл = Новый Файл(УбратьКавычки(пИмяФайла));
		
		путь = файл.ПолноеИмя;
		путь = СтрЗаменить(путь, "\", "/");

		Возврат путь;
		
	Исключение
		
		Сообщить("Ошибка получения абсолютного пути для " + пИмяФайла);
		Сообщить("	" + ОписаниеОшибки());
		Возврат пИмяФайла;
		
	КонецПопытки;
	
КонецФункции

Процедура ПодключитьМутантов() Экспорт

	Лог.Отладка("Подключаем мутантов");
	Для Каждого ФайлМутант Из НайтиФайлы("src/Мутанты", "*.os") Цикл

		Лог.Отладка("Подключаю " + ФайлМутант.ПолноеИмя + " как " + ФайлМутант.ИмяБезРасширения);
		ПодключитьСценарий(ФайлМутант.ПолноеИмя, ФайлМутант.ИмяБезРасширения);

	КонецЦикла;

КонецПроцедуры

Функция ДоступныеМутанты() Экспорт

	Результат = Новый Массив;

	Для Каждого ФайлМутант Из НайтиФайлы("src/Мутанты", "*.os") Цикл

		Имя = ФайлМутант.ИмяБезРасширения;
		Мутант = Вычислить("Новый " + Имя);
		Идентификатор = Мутант.Мутант();
		Описание = Мутант.Описание();

		Запись = НовыйЗаписьОМутантах(Имя, Идентификатор, Описание);
		Результат.Добавить(Запись);

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция НовыйЗаписьОМутантах(Класс, Идентификатор, Описание)

	Запись = Новый Структура("Класс, Идентификатор, Описание", Класс, Идентификатор, Описание);
	Возврат Запись;

КонецФункции

Функция УбратьКавычки(Знач пСтрока) Экспорт
	
	строкаБезКавычек = пСтрока;
	
	Если СтрНачинаетсяС(строкаБезКавычек, """") Тогда
		СтрокаБезКавычек = Сред(СтрокаБезКавычек, 2);
	КонецЕсли;
	
	Если СтрЗаканчиваетсяНа(строкаБезКавычек, """") Тогда
		СтрокаБезКавычек = Лев(СтрокаБезКавычек, СтрДлина(СтрокаБезКавычек) - 1);
	КонецЕсли;
	
	СтрокаБезКавычек = СтрЗаменить(СтрокаБезКавычек, """""", """");
	
	Возврат строкаБезКавычек;
	
КонецФункции

Процедура СоздатьФайлыСМутантами(НовыеИсходники, КаталогВыгрузки, Расширение) Экспорт

	Для Каждого ИмяКод Из НовыеИсходники Цикл

		НовыйПуть = КаталогВыгрузки + ИмяКод.Ключ + Расширение;
		Лог.Отладка("Сохраняем новый исходник по пути: " + НовыйПуть);

		Попытка

			ЗаписьТекста = Новый ЗаписьТекста(НовыйПуть, КодировкаТекста.UTF8);
			ЗаписьТекста.Записать(ИмяКод.Значение);
			ЗаписьТекста.Закрыть();

		Исключение

			_ОписаниеОшибки = ОписаниеОшибки();
			Лог.Ошибка("Не удалось записать файл. По причине: " + _ОписаниеОшибки);
			ЗавершитьРаботу(1);

		КонецПопытки;
	
	КонецЦикла;
	
КонецПроцедуры

Лог = ПараметрыПриложения.Лог();
