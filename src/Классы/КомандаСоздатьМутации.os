#Использовать logos
#Использовать fs

Перем Лог;

// Файл исходников с кодом для генерации
Перем Исходники;
// Расширение файла
Перем Расширение;
// Куда сохранять мутантов
Перем КаталогВыгрузки;
// Список мутаций
Перем Мутации;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("SRC", "", "Путь к файлу исходников. Например: module.bsl")
		.ТСтрока()
		.ВОкружении("MUTANT_SRC");

	Команда.Аргумент("OUTPUT", "", "Каталог для хранения результатов")
		.ТСтрока()
		.ВОкружении("MUTANT_RESULT");

	Команда.Опция("skip", "", "Пропуск мутаций. Необходимо указать ИДЕНТИФИКАТОР через запятую").ТСтрока();

	Команда.Опция("v verbose", Ложь, "Режим отладки")
		.ТБулево()
		.СкрытьВСправке();

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	ИнициализацияПараметров(Команда);
	
	ЧтениеТекста = Новый ЧтениеТекста(Исходники, КодировкаТекста.UTF8);
	ИсходныйКод = ЧтениеТекста.Прочитать();

	Файл = Новый Файл(Исходники);
	Расширение = Файл.Расширение;

	НовыеИсходники = Кодогенерация.ВыполнитьЗаменыВКоде(ИсходныйКод, Мутации);
	Лог.Отладка("Было создано " + НовыеИсходники.Количество() + " мутантов");

	Если НовыеИсходники.Количество() = 0 Тогда

		Лог.Информация("Мутации не были созданы");
		ЗавершитьРаботу(0);

	Иначе

		ОбщегоНазначения.СоздатьФайлыСМутантами(НовыеИсходники, КаталогВыгрузки, Расширение);

	КонецЕсли;

КонецПроцедуры

Процедура ИнициализацияПараметров(Знач Команда)

	Если Команда.ЗначениеОпции("verbose") Тогда

		Лог.УстановитьУровень(УровниЛога.Отладка);

	КонецЕсли;

	Исходники = Команда.ЗначениеАргумента("SRC");
	Лог.Отладка("Исходники для мутаций: " + Исходники);

	Если Не ФС.ФайлСуществует(Исходники) Тогда

		Лог.Ошибка(СтрШаблон("Нет файла исходников <%1>", Исходники));
		ЗавершитьРаботу(1);

	КонецЕсли;

	КаталогВыгрузки = Команда.ЗначениеАргумента("OUTPUT");
	Лог.Отладка("Каталог для сохранения результатов: " + КаталогВыгрузки);

	Если Не ФС.КаталогСуществует(КаталогВыгрузки) Тогда

		СоздатьКаталог(КаталогВыгрузки);

	КонецЕсли;

	Искл = Команда.ЗначениеОпции("skip");
	ИсключаемыеМутации = СтрРазделить(Искл, ",");

	Мутации = Новый Массив;
	Для Каждого Мутация Из ОбщегоНазначения.ДоступныеМутанты() Цикл

		Если ИсключаемыеМутации.Найти(Мутация.Идентификатор) <> Неопределено Тогда

			Лог.Информация("Исключена мутация: " + Мутация.Идентификатор);
			Продолжить;
		
		КонецЕсли;

		Мутации.Добавить(Вычислить("Новый " + Мутация.Класс));

	КонецЦикла;

	Если Мутации.Количество() = 0 Тогда

		Лог.Ошибка("Не осталось мутаций для применения");
		ЗавершитьРаботу(1);

	КонецЕсли;

КонецПроцедуры



Лог = ПараметрыПриложения.Лог();
