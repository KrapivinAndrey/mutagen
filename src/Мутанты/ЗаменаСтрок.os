
Перем Токены;
Перем ТаблицаЗамен;

Перем ТокенСтрока;

Функция Мутант() Экспорт

	Возврат "MagicStrings";

КонецФункции

Функция Описание() Экспорт

	Возврат "Замена строк в коде на новые";

КонецФункции

#Область Плагин

Процедура Открыть(Парсер, Параметры) Экспорт
	Токены = Парсер.Токены();
	ТаблицаЗамен = Парсер.ТаблицаЗамен();

	ТокенСтрока = Токены.Строка;

КонецПроцедуры // Открыть()

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьВыражениеЛитерал");
	Возврат Подписки;
КонецФункции // Подписки()

#КонецОбласти

#Область ВспомогательныеФункции

Процедура Вставка(Текст, Позиция, Длина)
	НоваяЗамена = ТаблицаЗамен.Добавить();
	НоваяЗамена.Источник = Мутант();
	НоваяЗамена.Текст = Текст;
	НоваяЗамена.Позиция = Позиция;
	НоваяЗамена.Длина = Длина;
КонецПроцедуры

#КонецОбласти

#Область Подписки

Процедура ПосетитьВыражениеЛитерал(ВыражениеЛитерал) Экспорт 
	
	Если ВыражениеЛитерал.Вид = ТокенСтрока Тогда

		Длина = ВыражениеЛитерал.Начало.Длина;
		НоваяСтрока = "xx" + ВыражениеЛитерал.Значение + "xx";
		Вставка(НоваяСтрока, ВыражениеЛитерал.Начало.Позиция + 1, Длина - 2);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти