
Перем Токены;
Перем ТаблицаЗамен;

Перем ТокенЧисло, ТокенИстина, ТокенЛожь, ТокенСтрока, ТокенНеопределено;

Функция Мутант() Экспорт

	Возврат "ParamByDefault";

КонецФункции

Функция Описание() Экспорт

	Возврат "Замена параметов по умолчанию";

КонецФункции

#Область Плагин

Процедура Открыть(Парсер, Параметры) Экспорт
	Токены = Парсер.Токены();
	ТаблицаЗамен = Парсер.ТаблицаЗамен();

	ТокенЧисло	= Токены.Число;
	ТокенИстина = Токены.Истина;
	ТокенЛожь	= Токены.Ложь;
	ТокенСтрока = Токены.Строка;
	ТокенНеопределено = Токены.Неопределено;

КонецПроцедуры // Открыть()

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;

	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьОбъявлениеПараметра");

	Возврат Подписки;
КонецФункции // Подписки()

#КонецОбласти

#Область ВспомогательныеФункции

Процедура Вставка(Текст, Позиция, Длина)
	НоваяЗамена = ТаблицаЗамен.Добавить();
	НоваяЗамена.Источник = Мутант();
	НоваяЗамена.Текст = Текст;
	НоваяЗамена.Позиция = Позиция;
	НоваяЗамена.Длина = Длина;
КонецПроцедуры

#КонецОбласти

#Область Подписки

Процедура ПосетитьОбъявлениеПараметра(ОбъявлениеПараметра) Экспорт 

	Если ОбъявлениеПараметра.Значение = Неопределено Тогда

		Возврат;

	КонецЕсли;

	Если ЕстьЛиКолонкаВСтрокеТЗ("Элементы", ОбъявлениеПараметра.Значение) Тогда
		// Обработка когда строка в параметры передана
		Элемент = ОбъявлениеПараметра.Значение.Элементы[0];

	Иначе

		Элемент = ОбъявлениеПараметра.Значение;

	КонецЕсли;

	Позиция	= Элемент.Начало.Позиция;
	Длина	= Элемент.Начало.Длина;
	Вид 	= Элемент.Вид;
	Значение = Элемент.Значение;

	Если Вид = ТокенЧисло Тогда

		Сдвиг = 5;
		
		НовоеЧисло = Формат(Значение + Сдвиг, "ЧРД=.; ЧН=0; ЧГ=");
		Вставка(НовоеЧисло, Позиция, Длина);

	ИначеЕсли Вид = ТокенИстина Тогда

		Вставка("Ложь", Позиция, 6);

	ИначеЕсли Вид = ТокенЛожь Тогда

		Вставка("Истина", Позиция, 4);

	ИначеЕсли Вид = ТокенСтрока Тогда

		НоваяСтрока = "xx" + Значение + "xx";
		Вставка(НоваяСтрока, Позиция + 1, Длина - 2);

	ИначеЕсли Вид = ТокенНеопределено Тогда

		Вставка("Null", Позиция, 12);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Функция ЕстьЛиКолонкаВСтрокеТЗ(ИмяКолонки, СтрокаТаблицыЗначений)
	УИД = Новый УникальныйИдентификатор(); 
	ВременнаяСтруктура = Новый Структура(ИмяКолонки, УИД); 
	ЗаполнитьЗначенияСвойств(ВременнаяСтруктура, СтрокаТаблицыЗначений); 
	Возврат ВременнаяСтруктура[ИмяКолонки] <> УИД;
КонецФункции