
Перем Токены;
Перем ТаблицаЗамен;

Перем ТокенЧисло, ТокенИстина, ТокенЛожь, ТокенСтрока;

Функция Мутант() Экспорт

	Возврат "ParamByDefault";

КонецФункции

Функция Описание() Экспорт

	Возврат "Замена параметов по умолчанию";

КонецФункции

#Область Плагин

Процедура Открыть(Парсер, Параметры) Экспорт
	Токены = Парсер.Токены();
	ТаблицаЗамен = Парсер.ТаблицаЗамен();

	ТокенЧисло	= Токены.Число;
	ТокенИстина = Токены.Истина;
	ТокенЛожь	= Токены.Ложь;
	ТокенСтрока = Токены.Строка;

КонецПроцедуры // Открыть()

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;

	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьОбъявлениеПараметра");

	Возврат Подписки;
КонецФункции // Подписки()

#КонецОбласти

#Область ВспомогательныеФункции

Процедура Вставка(Текст, Позиция, Длина)
	НоваяЗамена = ТаблицаЗамен.Добавить();
	НоваяЗамена.Источник = Мутант();
	НоваяЗамена.Текст = Текст;
	НоваяЗамена.Позиция = Позиция;
	НоваяЗамена.Длина = Длина;
КонецПроцедуры

#КонецОбласти

#Область Подписки

Процедура ПосетитьОбъявлениеПараметра(ОбъявлениеПараметра) Экспорт 

	Если ОбъявлениеПараметра.Значение = Неопределено Тогда

		Возврат;

	КонецЕсли;

	Позиция	= ОбъявлениеПараметра.Значение.Начало.Позиция;
	Длина	= ОбъявлениеПараметра.Значение.Начало.Длина;
	Значение = ОбъявлениеПараметра.Значение.Значение;
	Вид 	= ОбъявлениеПараметра.Значение.Вид;

	Если Вид = ТокенЧисло Тогда

		Сдвиг = 5;
		
		НовоеЧисло = Формат(Значение + Сдвиг, "ЧРД=.; ЧН=0; ЧГ=");
		Вставка(НовоеЧисло, Позиция, Длина);

	ИначеЕсли Вид = ТокенИстина Тогда

		Вставка("Ложь", Позиция, 6);

	ИначеЕсли Вид = ТокенЛожь Тогда

		Вставка("Истина", Позиция, 4);

	ИначеЕсли Вид = ТокенСтрока Тогда

			НоваяСтрока = "xx" + Значение + "xx";
			Вставка(НоваяСтрока, Позиция + 1, Длина - 2);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти