
Перем Токены;
Перем ТаблицаЗамен;

Перем ТокенЧисло;

Функция Мутант() Экспорт

	Возврат "MagicNumbers";

КонецФункции

Функция Описание() Экспорт

	Возврат "Замена цифр в коде на новые"

КонецФункции

#Область Плагин

Процедура Открыть(Парсер, Параметры) Экспорт
	Токены = Парсер.Токены();
	ТаблицаЗамен = Парсер.ТаблицаЗамен();

	ТокенЧисло = Токены.Число;

КонецПроцедуры // Открыть()

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьВыражениеЛитерал");
	Возврат Подписки;
КонецФункции // Подписки()

#КонецОбласти

#Область ВспомогательныеФункции

Процедура Вставка(Текст, Позиция, Длина)
	НоваяЗамена = ТаблицаЗамен.Добавить();
	НоваяЗамена.Источник = Мутант();
	НоваяЗамена.Текст = Текст;
	НоваяЗамена.Позиция = Позиция;
	НоваяЗамена.Длина = Длина;
КонецПроцедуры

#КонецОбласти

#Область Подписки

Процедура ПосетитьВыражениеЛитерал(ВыражениеЛитерал) Экспорт 
	
	Сдвиг = 5;
	Если ВыражениеЛитерал.Вид = ТокенЧисло Тогда

		Длина = ВыражениеЛитерал.Начало.Длина;
		НовоеЧисло = Формат(ВыражениеЛитерал.Значение + Сдвиг, "ЧРД=.; ЧН=0; ЧГ=");
		Вставка(НовоеЧисло, ВыражениеЛитерал.Начало.Позиция, Длина);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти