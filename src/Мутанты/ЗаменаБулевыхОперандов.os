
Перем Токены;
Перем ТаблицаЗамен;

Перем ТокенИ, ТокенИЛИ;

Функция Мутант() Экспорт

	Возврат "SwitchBoolOperator";

КонецФункции

Функция Описание() Экспорт

	Возврат "Замена булевых операндов И/ИЛИ/НЕ"

КонецФункции

#Область Плагин

Процедура Открыть(Парсер, Параметры) Экспорт
	Токены = Парсер.Токены();
	ТаблицаЗамен = Парсер.ТаблицаЗамен();

	ТокенИ = Токены.И;
	ТокенИЛИ = Токены.ИЛИ;

КонецПроцедуры // Открыть()

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьВыражениеБинарное");
	Подписки.Добавить("ПосетитьВыражениеНе");
	Возврат Подписки;
КонецФункции // Подписки()

#КонецОбласти

#Область ВспомогательныеФункции

Процедура Вставка(Текст, Позиция, Длина)
	НоваяЗамена = ТаблицаЗамен.Добавить();
	НоваяЗамена.Источник = Мутант();
	НоваяЗамена.Текст = Текст;
	НоваяЗамена.Позиция = Позиция;
	НоваяЗамена.Длина = Длина;
КонецПроцедуры

#КонецОбласти

#Область Подписки

Процедура ПосетитьВыражениеБинарное(ВыражениеБинарное) Экспорт 

	Если ВыражениеБинарное.Операция.Токен = ТокенИ Тогда

		Вставка("ИЛИ", ВыражениеБинарное.Операция.Позиция, 1);

	ИначеЕсли ВыражениеБинарное.Операция.Токен = ТокенИЛИ Тогда

		Вставка("И", ВыражениеБинарное.Операция.Позиция, 3);

	КонецЕсли;

КонецПроцедуры

Процедура ПосетитьВыражениеНе(ВыражениеНе) Экспорт

	Вставка("", ВыражениеНе.Начало.Позиция, 2);

КонецПроцедуры

#КонецОбласти