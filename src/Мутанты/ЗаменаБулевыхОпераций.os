
Перем Токены;
Перем ТаблицаЗамен;

Перем ТокеныБулевыхОпераций;

Функция Мутант() Экспорт

	Возврат "SwitchBool";

КонецФункции

Функция Описание() Экспорт

	Возврат "Замена булевых операций в выражениях. | = -> <> | > -> < |"

КонецФункции

#Область Плагин

Процедура Открыть(Парсер, Параметры) Экспорт
	Токены = Парсер.Токены();
	ТаблицаЗамен = Парсер.ТаблицаЗамен();

	ТокеныБулевыхОпераций = Новый Массив();
	ТокеныБулевыхОпераций.Добавить(Токены.ЗнакРавно);
	ТокеныБулевыхОпераций.Добавить(Токены.ЗнакНеРавно);
	ТокеныБулевыхОпераций.Добавить(Токены.ЗнакМеньше);
	ТокеныБулевыхОпераций.Добавить(Токены.ЗнакБольше);
	ТокеныБулевыхОпераций.Добавить(Токены.ЗнакМеньшеИлиРавно);
	ТокеныБулевыхОпераций.Добавить(Токены.ЗнакБольшеИлиРавно);

КонецПроцедуры // Открыть()

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьВыражениеБинарное");
	Возврат Подписки;
КонецФункции // Подписки()

#КонецОбласти

#Область ВспомогательныеФункции

Функция ЭтоБулеваяОперация(Токен)

	Результат = ТокеныБулевыхОпераций.Найти(Токен) <> Неопределено;

	Возврат Результат;

КонецФункции

Функция ЗаменаОперации(Токен)

	Замены = Новый Соответствие();
	// =	-> <>
	// <>	-> =
	// >	-> <
	// <	-> >
	// >=	-> <=
	// <=	-> >=

	Замены.Вставить(Токены.ЗнакРавно,			"<>");
	Замены.Вставить(Токены.ЗнакНеРавно,			"=");
	Замены.Вставить(Токены.ЗнакБольше,			"<");
	Замены.Вставить(Токены.ЗнакМеньше,			">");
	Замены.Вставить(Токены.ЗнакБольшеИлиРавно,	"<=");
	Замены.Вставить(Токены.ЗнакМеньшеИлиРавно,	">=");

	Результат = Замены.Получить(Токен);

	Возврат Результат;

КонецФункции

Функция ДлинаТокена(Замена)

	Если Замена.Токен = Токены.ЗнакРавно Тогда
		Результат = 1;
	ИначеЕсли Замена.Токен = Токены.ЗнакНеРавно Тогда
		Результат = 2;
	ИначеЕсли Замена.Токен = Токены.ЗнакБольше Тогда
		Результат = 1;
	ИначеЕсли Замена.Токен = Токены.ЗнакМеньше Тогда
		Результат = 1;
	ИначеЕсли Замена.Токен = Токены.ЗнакБольшеИлиРавно Тогда
		Результат = 2;
	ИначеЕсли Замена.Токен = Токены.ЗнакМеньшеИлиРавно Тогда
		Результат = 2;
	Иначе
		ВызватьИсключение "Неопознанные токен" + Замена.Токен;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура Вставка(Текст, Позиция, Длина)
	НоваяЗамена = ТаблицаЗамен.Добавить();
	НоваяЗамена.Источник = Мутант();
	НоваяЗамена.Текст = Текст;
	НоваяЗамена.Позиция = Позиция;
	НоваяЗамена.Длина = Длина;
КонецПроцедуры

#КонецОбласти

#Область Подписки

Процедура ПосетитьВыражениеБинарное(ВыражениеБинарное) Экспорт 

	Если ЭтоБулеваяОперация(ВыражениеБинарное.Операция.Токен) Тогда

		НовыйТокен = ЗаменаОперации(ВыражениеБинарное.Операция.Токен);
		Длина = ДлинаТокена(ВыражениеБинарное.Операция);
		Вставка(НовыйТокен, ВыражениеБинарное.Операция.Позиция, Длина);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти